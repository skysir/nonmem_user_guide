; SSMULTIDOSE.CTL 
;
$PROBLEM DOSE SUPERPOSITION WITH STEADY STATE, USING MULTIPLE BOLUS DOSES
; THIS IS AN EXAMPLE OF STEADY STATE WITH MULTIPLE BOLUS  DOSES 
; INTO A TRANSIT COMPARTMENT.
; THE NUMBER OF DOSE RECORDS IN ssmultidose.dat IS SET ARBITRARILY TO 9.
; COMPARE OUTPUT WITH SSONEDOSE.CTL
; USES NEW FEATURES OF NM73
;
$INPUT ID TIME CMT AMT DV 
$DATA ssmultidose.dat IGNORE=@
$SUBROUTINES ADVAN6 TOL=10 TRANS1

$MODEL COMP=ABS; (DEFDOSE)
       COMP=CENTRAL ;(DEFOBS)
       COMP=PERI
 
$ABBR DECLARE DOSETIME(100),DOSE(100)
$ABBR DECLARE DOWHILE I
$ABBR DECLARE DOWHILE NDOSE

$PK 
CALLFL=-2
IF (NEWIND<2) NDOSE=0
IF (AMT > 0 .AND. CMT==1) THEN
 NDOSE=NDOSE+1
 DOSETIME(NDOSE)=TIME
 DOSE(NDOSE)=AMT
ENDIF

CL = THETA(1)*EXP(ETA(1))
V2 = THETA(2)*EXP(ETA(2))
Q  = THETA(3)
V3 = THETA(4)

K=CL/V2
S2 =V2
K23=Q/V2
K32=Q/V3

;--------ABSORPTION MODEL-----------
F1=0
MTT  = THETA(5)*EXP(ETA(4))
NN   = THETA(6)*EXP(ETA(5))
BIO  = THETA(7)*EXP(ETA(3))
KTR  = (NN+1)/MTT

NFAC=SQRT(2*3.1416)*NN**(NN+0.5)*(EXP(-NN))*(1+1/(12*NN))
KINPT=BIO*KTR**(NN+1)/NFAC

$DES
INPT=0
I=1
DOWHILE (I<=NDOSE)
IPT=0
IF (T>=DOSETIME(I)) IPT=DOSE(I)*(T-DOSETIME(I))**NN*EXP(-KTR*(T-DOSETIME(I)))
INPT=INPT+IPT
I=I+1
ENDDO

 DADT(1)=KINPT*INPT-KTR*A(1)
 DADT(2)=KTR*A(1)-K23*A(2)-K*A(2)+K32*A(3)
 DADT(3)=K23*A(2)-K32*A(3)

$ERROR

Y = F*EXP(EPS(1))

$THETA 
4 ;CL
3 ;V2
1 ;Q
2 ;V3
10 ;MTT
5 ;NN
0.85 ;BIO

$OMEGA 1 1 1 1 1 
$SIGMA 1 
$TABLE  ID TIME FORMAT=S1PE18.11 FILE=ssmultidose.tab
