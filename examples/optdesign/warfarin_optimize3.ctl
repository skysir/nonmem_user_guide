$PROBLEM WARFARIN
$INPUT ID TIME AMT RATE EVID MDV DV TSTRAT TMIN TMAX
$DATA warfarin_opt.csv ignore=@

$SUBROUTINES ADVAN2 TRANS2

$PK
MU_1=LOG(THETA(1))
MU_2=LOG(THETA(2))
MU_3=LOG(THETA(3))
CL=THETA(1)*EXP(ETA(1))
V=THETA(2)*EXP(ETA(2))
KA=THETA(3)*EXP(ETA(3))
S2=V
F1=1.0

$ERROR
IPRED=A(2)/V
Y=IPRED*(1.0+EPS(1)) + EPS(2)

$THETA
0.15 ;[CL]
8.0  ;[V]
1.0  ;[KA]

;$OMEGA (0.0049) (0.0004) (0.36)
;$SIGMA (0.0001)

$OMEGA (0.07 ) (0.02 ) (0.6 )
$SIGMA (0.01 ) (0.0001 FIXED )

$DESIGN RS FIMDIAG=1 GROUPSIZE=32 OFVTYPE=0 
           APPROX=FOI SIGL=10 NOHABORT PRINT=100 MAXEVAL=400
           DESEL=TIME DESELSTRAT=TSTRAT DESELMIN=TMIN DESELMAX=TMAX
$DESIGN STGR DESEL=TIME DESELSTRAT=TSTRAT DESELMIN=TMIN DESELMAX=TMAX
$DESIGN NELDER DESEL=TIME DESELSTRAT=TSTRAT DESELMIN=TMIN DESELMAX=TMAX
$TABLE ID TIME EVID MDV DV IPRED NOPRINT NOAPPEND FILE=warfarin_optimize3.tab  FORMAT=S1PE23.16

