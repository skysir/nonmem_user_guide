$PROB delayed time example
$INPUT WWX TIME  DV AMT RATE CMT EVID MDV
$DATA delayed.csv IGNORE=C

$SUBROUTINES ADVAN13 TRANS1 TOL=8
$MODEL NCOMPARTMENTS=5

$PK
TDELAY=THETA(1)
ALAG3=TDELAY
KEL=THETA(2)
K0=THETA(3)
K1=THETA(4)
SC50=THETA(5)
SMAX=THETA(6)
V=THETA(7)
KS=THETA(8)
A_0(2)=K0/K1
A_0(4)=K0/K1
PI=3.141592654E+00

$DES
DADT(1) = -KEL*A(1)
DADT(2) = K0*(1.0 + SMAX*A(1)/(SC50*V + A(1))) - K1*A(2) &
        + KS*A(2)*SIN(2.0d+00*PI*T/TDELAY)*TDELAY/(2.0d+00*PI)
DADT(3) = -KEL*A(3)
DADT(4) = K0*(1.0 + SMAX*A(3)/(SC50*V + A(3))) - K1*A(4) &
        + KS*A(4)*SIN(2.0d+00*PI*T/TDELAY)*TDELAY/(2.0d+00*PI)
; use this conditional statement to "prime the system", and
; facilitate evaluating initial condition of A(3)
IF(T.lt.TDELAY) THEN
DADT(5)=K1*A(2)
ELSE
DADT(5)=K1*A(2) - K1*A(4)
ENDIF

$ERROR
IPRED=LOG(ABS(A(5))+1.0E-04)
Y=IPRED+ERR(1)

$THETA
;(0.001,23.4155) (0.25 FIXED) (0.0,0.309388) (0.0,0.0497557) (0.0,1.15534) (0.0,72.72273) (1.0 FIXED) (0.05 FIXED)
(0.001,15) (0.25 FIXED) (0.0,0.7) (0.0,0.08) (0.0,2) (0.0,45) (1.0 FIXED) (0.05 FIXED)

$OMEGA 0.01

$EST METHOD=0 PRINT=10 MAXEVAL=1000 NSIG=4
$COV UNCONDITIONAL MATRIX=R
$TABLE TIME DV IPRED NOAPPEND NOPRINT FILE=delayed.tab
