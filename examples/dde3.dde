; Pre-Control stream template dde3.dde used by ddexpand program to form functional NMTRAN control stream dde3.ctl
$PROB DDE Problem

; the data file should have only DOSE input records pertaining to base equations.  Also, the CMT must be a data item
; The ddexpand program, using finedata's EXTRADOSE facility, will add doses for additional compartments
; and call the new data file dde3_dde.csv
$INPUT ID TIME    AMT    RATE   CMT   EVID MDV  DV
$DATA dde3.csv IGNORE=C
$SUBROUTINES ADVAN13 TRANS1 TOL=12
$MODEL NCOMPARTMENTS=12 ; number of compartments must be adjusted by user after ddexpand is executed.

$PK
CEVID=EVID
IF(CMT/=1) CEVID=1
K10=THETA(1)+ETA(1)
K12=THETA(2)+ETA(2)
K21=THETA(3)+ETA(3)
V1=THETA(4)+ETA(4)
K1=THETA(5)+ETA(5)
K2=THETA(6)+ETA(6)
K4=THETA(7)+ETA(7)
K5=THETA(8)+ETA(8)
SIG1=THETA(9)+ETA(9)
SIG2=THETA(10)+ETA(10)
SIG3=THETA(11)+ETA(11)
;  TAU1, TAU2, TAU3,etc. are time delays.  This sample has one time delay, TAU1
TAU1=THETA(12)+ETA(12)
I0=THETA(13)+ETA(13)
K3=5.0
AA=1.0
BB=0.5
; Set initial conditions for Base equations
A_0(1)=AA
A_0(2)=I0
A_0(6)=AA
A_0(7)=I0
;Any propagations of initial conditions and ALAG's will be placed here by ddexpand program.

$DES
; AD_x_y is the State value of A(x) delayed for time TAUy.  These are used in the differntial equations later on.
; AP_x_y is the State value of A(x) in the past, for time delay TAUy.  That is, when T<Tauy, the AP_x_y defines A(x)
; For every AD_x_y used in the differential equations, there must be an AP_X_Y defined.
; If past is constant, then it can be as simple as AP_x_y=Initial condition constant (same value as A_0(x) is set to).
; Make sure AP_x_y is a function of T: do not use T-TAUy, as this will be done by the ddexpand program.
AP_1_1=AA*EXP(BB*T)
AP_6_1=AA*EXP(BB*T)
; BASE EQUATIONS ENTERED BY USER.  Note use of AD_1_1 and AD_6_1, which warrants an expansion.
DADT(1)=K3-(K1/K2)*(1.0-EXP(-K2*T))*A(1)
DADT(2)=K4*A(1)-K4*AD_1_1
DADT(3)=K4*AD_1_1-K5*A(3)
CC=A(4)/V1
EFFECT=CC*(SIG1*EXP(-SIG2*CC)+SIG3)
DADT(4)=-K10*A(4)-K12*A(4)+K21*A(5)
DADT(5)=K12*A(4)-K21*A(5)
DADT(6)=K3-EFFECT*A(6) - K1/K2* &
(1.0-EXP(-K2*T))*A(6)
DADT(7)=K4*A(6)-K4*AD_6_1
DADT(8)=K4*AD_6_1-K5*A(8)
;Any delay equations necessary are placed here by the ddexpand program.

$ERROR
A1=A(1)
A2=A(2)
A3=A(3)
A4=A(4)
A5=A(5)
A6=A(6)
A7=A(7)
A8=A(8)
A9=A(9)
A10=A(10)
A11=A(11)
A12=A(12)

Y1=A(2)+A(3)
Y2=A(7)+A(8)
Y3=A(3)
Y4=A(8)
IF(CMT==1) IPRED=Y1
IF(CMT==2) IPRED=Y2
IF(CMT==3) IPRED=Y3
IF(CMT==4) IPRED=Y4
IF(CMT==1) Y=IPRED*(1.0+EPS(1))
IF(CMT==1) Y=IPRED*(1.0+EPS(2))
IF(CMT==2) Y=IPRED*(1.0+EPS(3))
IF(CMT==4) Y=IPRED*(1.0+EPS(4))

$THETA
0.32544   ; 1: K10
2.6496    ; 2: K12
2.5944    ; 3: K21
0.02645   ; 4: V
0.456     ; 5: K1
0.169     ; 6: K2
0.185     ; 7: K4
0.031     ; 8: K5
0.328     ; 9: SIG1
0.328     ; 10: SIG2
0.025     ; 11: SIG3
10.6      ; 12: TAU1
2.83      ; 13: I0

$OMEGA (0.0 FIXED)X13
$SIGMA (0.04)X4
$SIMULATION (567811 NORMAL) (2933012 UNIFORM) ONLYSIMULATION SUBPROBLEMS=1
$TABLE TIME Y1 Y2 Y3 Y4 EXCLUDE_BY CEVID NOAPPEND NOPRINT FILE=dde3.tab
