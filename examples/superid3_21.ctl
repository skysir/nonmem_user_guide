$PROB RUN# 
$INPUT C ID TIME DV AMT RATE EVID MDV CMT ROWNUM SID
$DATA superid3.csv IGNORE=C

$SUBROUTINES ADVAN2 TRANS2

$PK
MU_4=THETA(1)
MU_5=THETA(2)
MU_6=THETA(3)
KA=DEXP(MU_4+ETA(4)+ETA(1))
CL=DEXP(MU_5+ETA(5)+ETA(2))
V=DEXP(MU_6+ETA(6)+ETA(3))
S2=V

$ERROR
IPRE=F
Y = IPRE + IPRE*EPS(1)

; Initial values of THETA
$THETA 0.2 -4 -2
;INITIAL values of OMEGA
$OMEGA BLOCK(3)
0.1
0.001 0.1
0.001 0.001 0.1

$OMEGA BLOCK(3)
0.3
0.001 0.3
0.001 0.001 0.3

;Initial value of SIGMA
$SIGMA 
0.1     ;[P]

$PRIOR NWPRI 
$OMEGAP BLOCK(3)
0.01
0.001 0.01
0.001 0.001 0.01

$OMEGAP BLOCK(3)
0.03
0.001 0.03
0.001 0.001 0.03

$OMEGAPD (3 FIXED)X2

$LEVEL
SID=(4[1],5[2],6[3])

$EST METHOD=ITS AUTO=1 PRINT=1 SIGL=8 FNLETA=0 NOPRIOR=1
     LEVCENTER=0
$EST METHOD=IMP AUTO=1 PRINT=1 NOPRIOR=1
$EST METHOD=BAYES AUTO=1 PRINT=25 NITER=2000 NOPRIOR=0
$EST METHOD=1 PRINT=5 NSIG=3 SIGL=10 FNLETA=0 SLOW NONINFETA=1 NOPRIOR=1
$COV MATRIX=R UNCONDITIONAL
