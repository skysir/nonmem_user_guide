;Model Desc: covariate modeling included
;Project Name: sampc
;Project ID: NO PROJECT DESCRIPTION

$PROB RUN# sampc Setup of Data from ADAPT
$INPUT C SET ID JID TIME DV=CONC AMT=DOSE RATE EVID MDV CMT GNDR AGE
$DATA sampc_4.csv IGNORE=C
$SUBROUTINES ADVAN3 TRANS4

$PRIOR NWPRI NTHETA=11, NETA=4, NTHP=0, NETP=4, NPEXP=1

$PK
; LCLM stands for log tranformed clearance, male
LCLM=THETA(1)
LCLF=THETA(2)
; CLAM=CL age slope, male
CLAM=THETA(3)
CLAF=THETA(4)
; LV1M=log transformed V1, male
LV1M=THETA(5)
LV1F=THETA(6)
V1AM=THETA(7)
V1AF=THETA(8)
; LAGE=log transformed age
LAGE=DLOG(AGE)
LCLB=(1.0-GNDR)*(LCLM+LAGE*CLAM) + GNDR*(LCLF+LAGE*CLAF)
LV1B=(1.0-GNDR)*(LV1M+LAGE*V1AM) + GNDR*(LV1F+LAGE*V1AF)
MU1=LCLB
MU2=LV1B
MU3=THETA(9)
MU4=THETA(10)
CL=DEXP(MU1+ETA(1))
V1=DEXP(MU2+ETA(2))
Q=DEXP(MU3+ETA(3))
V2=DEXP(MU4+ETA(4))
S1=V1

$ERROR
CALLFL=0
SDSL=THETA(11)
W=F*SDSL
Y = F + W*EPS(1)
IPRED=F
IWRES=(DV-F)/W

$THETA
;  (0.0001 2.0 10000.0) (0.0001 2.0 10000.0) (-100000 2.0 100000.0) (-10000.0 2.0 100000.0) 
;   (0.0001 2.0 10000.0) (0.0001 2.0 10000.0) (-10000 2.0 10000) (-10000 2.0 100000)
;    (0.00001 2.0 10000.0) (0.0001 2.0 10000.0)
;    (0.001 0.3 1000.0)

( 0.7 ) ;[LCLM]
( 0.7 ) ;[LCLF]
( 2 )   ;[CLAM]
( 2.0);[CLAF]
( 0.7 ) ;[LV1M]
( 0.7 ) ;[LV1F]
( 2.0 )   ;[V1AM]
( 2.0 )   ;[V1AF]
( 0.7 ) ;[MU3]
(  0.7 );[MU4]
( 0.3 )     ;[SDSL]

;  (-10 3.0 10.0) (-10 3.0 10.0) (-10 -0.5 10.0) (-10.0 -0.3 10.0) 
;    (-10 0.7 10.0) (-10 0.7 10.0) (-10 0.3 10) (-10 0.2 10)
;    (-10 0.7 10.0) (-10  2.3 10.0)
;     (0.01 0.1 10.0)

$THETA 4 FIX

$OMEGA BLOCK(4)
0.5
0.001 0.5
0.001 0.001 0.5
0.001 0.001 0.001 0.5

$OMEGA BLOCK(4)
0.01 FIX 
0.0  0.01 
0.0 0.0 0.01 
0.0 0.0 0.0 0.01

$SIGMA 
(1.0 FIXED)


$EST METHOD=ITS INTERACTION FILE=sampc.ext NITER=1000 NSIG=2 PRINT=5 NOABORT MSFO=samp5l.msf SIGL=8 NOPRIOR=1
     CTYPE=3 CITER=5 CALPHA=0.05  GRD=GGGGGGGGGGS
$EST METHOD=IMP INTERACTION EONLY=1 NITER=5 ISAMPLE=3000 PRINT=1 SIGL=8 SEED=123334
     CTYPE=3 CITER=5 CALPHA=0.05
$EST METHOD=SAEM NBURN=3000 NITER=2000 PRINT=10 ISAMPLE=2
     CTYPE=3 CITER=5 CALPHA=0.05
$EST METHOD=IMP INTERACTION EONLY=1 NITER=5 ISAMPLE=3000 PRINT=1 SIGL=8 SEED=123334
$EST METHOD=IMP INTERACTION EONLY=0 NITER=20 ISAMPLE=1000 PRINT=1 SIGL=8
     CTYPE=3 CITER=5 CALPHA=0.05 GRD=GGGGGGGGGGS
$EST METHOD=BAYES INTERACTION FILE=sampc.TXT NBURN=10000 NITER=3000 PRINT=100 NOPRIOR=0
     CTYPE=3 CITER=5 CALPHA=0.05
$EST  METHOD=COND INTERACTION MAXEVAL=9999 FILE=sampc.ex2 NSIG=2 SIGL=14 PRINT=5 NOABORT NOPRIOR=1
$COV MATRIX=R UNCONDITIONAL
