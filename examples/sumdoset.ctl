; From Appendix 3 of 
; Jun Shen, Alison Boeckmann & Andrew Vick
; Journal of Pharmacokinetics and Pharmacodynamics
; ISSN 1567-567X Volume 39 Number 3
; J Pharmacokinet Pharmacodyn (2012) 39:251-262 DOI 10.1007/s10928-012-9247-3

$PROBLEM Dose Superposition
$INPUT ID TIME CMT AMT DOSENO DV ADDL II RATE
$DATA sumdoset.csv  IGNORE=@
$SUBROUTINES ADVAN6 TOL=10 TRANS1 

$MODEL COMP=ABS ;(DEFDOSE)
       COMP=CENTRAL;(DEFOBS)
       COMP=PERI

$PK 

CALLFL=-2
IF (NEWIND < 2) THEN
T1=0
T2=0
T3=0
T4=0
T5=0
T6=0
T7=0
DOSE1=0
DOSE2=0
DOSE3=0
DOSE4=0
DOSE5=0
DOSE6=0
DOSE7=0
ENDIF
IF (DOSENO == 1) THEN
 T1=TIME
 DOSE1=AMT
ENDIF
IF (DOSENO == 2) THEN
 T2=TIME
 DOSE2=AMT
ENDIF
IF (DOSENO == 3) THEN
 T3=TIME
 DOSE3=AMT
ENDIF
IF (DOSENO == 4) THEN
 T4=TIME
 DOSE4=AMT
ENDIF
IF (DOSENO == 5) THEN
 T5=TIME
 DOSE5=AMT
ENDIF
IF (DOSENO == 6) THEN
 T6=TIME
 DOSE6=AMT
ENDIF
IF (DOSENO == 7) THEN
 T7=TIME
 DOSE7=AMT
ENDIF
; Add more IF (DOSENO == ?) THEN blocks for more doses, as needed.

CL = THETA(1)*EXP(ETA(1))
V2 = THETA(2)*EXP(ETA(2))
Q  = THETA(3)
V3 = THETA(4)

K=CL/V2
S2 =V2
K23=Q/V2
K32=Q/V3

;--------ABSORPTION MODEL-----------
F1=0
MTT  = THETA(5)*EXP(ETA(4))
NN   = THETA(6)*EXP(ETA(5))
BIO  = THETA(7)*EXP(ETA(3))
KTR  = (NN+1)/MTT

NFAC=SQRT(2*3.1416)*NN**(NN+0.5)*(EXP(-NN))*(1+1/(12*NN))
KINPT=BIO*KTR**(NN+1)/NFAC

$DES
IPT1=0
IPT2=0
IPT3=0
IPT4=0
IPT5=0
IPT6=0
IPT7=0
; Add more IPT?=0 lines for more doses, as needed.

IF (T>=T1) IPT1=DOSE1*(T-T1)**NN*EXP(-KTR*(T-T1))
IF (T>=T2.AND.DOSE2>0.) IPT2=DOSE2*(T-T2)**NN*EXP(-KTR*(T-T2))
IF (T>=T3.AND.DOSE3>0.) IPT3=DOSE3*(T-T3)**NN*EXP(-KTR*(T-T3))
IF (T>=T4.AND.DOSE4>0.) IPT4=DOSE4*(T-T4)**NN*EXP(-KTR*(T-T4))
IF (T>=T5.AND.DOSE5>0.) IPT5=DOSE5*(T-T5)**NN*EXP(-KTR*(T-T5))
IF (T>=T6.AND.DOSE6>0.) IPT6=DOSE6*(T-T6)**NN*EXP(-KTR*(T-T6))
IF (T>=T7.AND.DOSE7>0.) IPT7=DOSE7*(T-T7)**NN*EXP(-KTR*(T-T7))
; Add more IF (T?>=... lines for more doses, as needed.

INPT=IPT1+IPT2+IPT3+IPT4+IPT5+IPT6+IPT7
; Add more +IPT? summands for more doses, as needed.

 DADT(1)=KINPT*INPT-KTR*A(1)
 DADT(2)=KTR*A(1)-K23*A(2)-K*A(2)+K32*A(3)
 DADT(3)=K23*A(2)-K32*A(3)

$ERROR

Y = F*EXP(EPS(1))

$THETA 
4 ;CL
3 ;V2
1 ;Q
2 ;V3
10 ;MTT
5 ;NN
0.85 ;BIO

$OMEGA 0 FIX 0 FIX 0 FIX 0 FIX 0 FIX 
$SIGMA 0 FIX
$SIMULATION (123456) ONLY
$TABLE  ID TIME CMT CL V2 Q V3 MTT NN INPT ONEHEADER NOPRINT FORMAT=s1PE18.11 FILE=abbrtable.txt

