$SIZES LTH=60
$PROB RUN# 
$INPUT C ID TIME DV AMT RATE EVID MDV CMT ROWNUM SID TYPE L2
$DATA superid3_6.csv IGNORE=C

$SUBROUTINES ADVAN2 TRANS2
$ABBR REPLACE THETA(SID_KA)=THETA(,4 to 19)
$ABBR REPLACE THETA(SID_CL)=THETA(,20 to 35)
$ABBR REPLACE THETA(SID_V)=THETA(,36 to 51)
$ABBR DECLARE DOWHILE I
$ABBR DECLARE INTEGER NSID

$PK
MU_1=THETA(1)
MU_2=THETA(2)
MU_3=THETA(3)
NSID=16
THSUM_KA=0.0
THSUM_CL=0.0
THSUM_V=0.0
I=1
DO WHILE (I<=NSID)
THSUM_KA=THSUM_KA-THETA(I+3)
THSUM_CL=THSUM_CL-THETA(I+19)
THSUM_V=THSUM_V-THETA(I+35)
I=I+1
ENDDO

IF(SID<NSID) THEN
KA=DEXP(MU_1+ETA(1)+THETA(SID_KA))
CL=DEXP(MU_2+ETA(2)+THETA(SID_CL))
V=DEXP(MU_3+ETA(3)+THETA(SID_V))
ELSE
KA=DEXP(MU_1+ETA(1)+THSUM_KA)
CL=DEXP(MU_2+ETA(2)+THSUM_CL)
V=DEXP(MU_3+ETA(3)+THSUM_V)
ENDIF

S2=V


$ERROR
IPRE=F
IF(TYPE==0) Y = IPRE + IPRE*EPS(1)
IF(TYPE==1.AND.SID<NSID) Y=THETA(SID_KA)+EPS(2)
IF(TYPE==1.AND.SID==NSID) Y=THSUM_KA+EPS(2)
IF(TYPE==2.AND.SID<NSID) Y=THETA(SID_CL)+EPS(3)
IF(TYPE==2.AND.SID==NSID) Y=THSUM_CL+EPS(3)
IF(TYPE==3.AND.SID<NSID) Y=THETA(SID_V)+EPS(4)
IF(TYPE==3.AND.SID==NSID) Y=THSUM_V+EPS(4)

; Initial values of THETA
$THETA 0.2 -4 -2 
       (0.1)x15 (0.0 FIXED)
       (0.1)x15 (0.0 FIXED)
       (0.1)x15 (0.0 FIXED)
;INITIAL values of OMEGA
$OMEGA BLOCK(3) VALUES(0.1,0.001)
;$OMEGA (0.0 FIXED)

;Initial value of SIGMA
$SIGMA 
0.1     ;[P]

;$SIGMA 0.3 0.3 0.3
$SIGMA BLOCK(3) VALUES(0.3,0.001)


;$EST METHOD=ITS INTERACTION PRINT=1 NSIG=2 NITER=500 SIGL=8 FNLETA=0 NOHABORT CTYPE=3 MCETA=10
;$EST METHOD=IMP INTERACTION PRINT=1 NSIG=2 NITER=500 SIGL=8 FNLETA=0 NOABORT CTYPE=3 MCETA=0 ISAMPLE=300 MAPITER=0
;$EST METHOD=SAEM INTERACTION PRINT=10 NSIG=2 NITER=100 SIGL=8 FNLETA=0 NOABORT CTYPE=3 MCETA=0 ISAMPLE=2 CONSTRAIN=0
;$EST METHOD=IMP EONLY=1 INTERACTION PRINT=1 NSIG=2 NITER=5 SIGL=8 FNLETA=0 NOABORT CTYPE=3 MCETA=0 ISAMPLE=300 MAPITER=0
;$EST METHOD=BAYES INTERACTION PRINT=10 NSIG=2 NBURN=1000 NITER=500 SIGL=8 FNLETA=0 NOABORT CTYPE=3
$EST METHOD=1 INTERACTION PRINT=1 NSIG=2 SIGL=10 FNLETA=0 NOHABORT NONINFETA=1 MCETA=20
$COV MATRIX=R UNCONDITIONAL SIGL=10
;$TABLE  ID TIME DV AMT RATE EVID MDV CMT SID KA CL V ETA1 ETA2 ETA3 ETA4 ETA5 ETA6 
;NOAPPEND ONEHEADER FILE=superid3_6.tab  NOPRINT
;$TABLE  ID SID KA CL V ETA1 ETA2 ETA3 ETA4 ETA5 ETA6 NOAPPEND ONEHEADER FILE=superid3_6.dat
;        FIRSTONLY NOPRINT FORMAT=,1PE15.8
