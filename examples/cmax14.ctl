

$PROB  THEOPHYLLINE   POPULATION DATA
$INPUT      ID DOSE=AMT TIME CP=DV WT
$DATA       THEOPPCMAX
$ABB COMRES=2
$ABBR DECLARE INTEGER I

$SUBROUTINES  ADVAN14  TOL=6 OTHER=cmax14u.f90
$MODEL COMP=(DEPOT,INITIALOFF,DEFDOSE) COMP=(CENTRAL,DEFOBS,NOOFF)

$PK

;THETA(1)=MEAN ABSORPTION RATE CONSTANT (1/HR)
;THETA(2)=MEAN ELIMINATION RATE CONSTANT (1/HR)
;THETA(3)=SLOPE OF CLEARANCE VS WEIGHT RELATIONSHIP (LITERS/HR/KG)
;SCALING PARAMETER=VOLUME/WT SINCE DOSE IS WEIGHT-ADJUSTED
   IF (Wt>0) WEIGHT=WT
   KA=THETA(1)+ETA(1)
   KE=THETA(2)+ETA(2)
   CL=THETA(3)*WEIGHT+ETA(3)
   S2=CL/KE/WEIGHT
 

$THETA  (0.0,1.96) (.0,.086) (0.0,.040)
$OMEGA BLOCK(3)  1.519 .00376 .000133 -0.066 .00752 .481

$DES

   DADT(1)=-KA*A(1)
   DADT(2)= KA*A(1)-KE*A(2)


$ERROR
include nonmem_reserved_general

TMAX=0.0
CMAX=0.0
ZRCROSSDIR=0 ; ZERO CROSSING DIRECTION
CMAXDER=0.0
; THIS ROOT-FINDER CAN FIND THE EXACT TMAX AND CMAX, BY FINDING WHEN DADT(2) IS ZERO
; SEE SUBROUTINE FCVROOTFN in cmax14u.f90
 IF(RTSIGNAL>0.0) THEN ; A ZERO-CROSSING HAS BEEN FOUND
  TMAX=TRTINFO(1) ; ONLY ONE CROSSING EXPECTED, SO INDEX IS 1
  CMAX=YRTINFO(2,1)/S2 ; PICK UP THE Y(2) VALUE AT CROSSING NUMBER 1
  CMAXDER=YPRTINFO(2,1); VERIFY THAT CMAX DERIVATIVE IS CLOSE TO ZERO.
  ZRCROSSDIR=RTINFO(1,1) ; VERIFY THAT ZEROCROSSING IS IN NEGATIVE DIRECTION (-1, FROM POSITIVE TO NEGATIVE), INDICATING A PEAK, RATHER THAN A NADIR
 RTSIGNAL=0
 ENDIF

; Compare with analytical solution for tmax, cmax
CMAXC=0.0
TMAXC=0.0
IF(AMT>0.0) THEN
  TMAXC=LOG(KA/KE)/(KA-KE)
  CMAXC=KA*AMT/S2*(EXP(-KE*TMAXC)-EXP(-KA*TMAXC))/(KA-KE)
ENDIF


IPRED=F
   Y=F+EPS(1)

$SIGMA  .476

$EST METHOD=1 MAXEVAL=9999 PRINT=1 SIGL=6 NOABORT
     NOTHETABOUNDTEST NOOMEGABOUNDTEST NOSIGMABOUNDTEST
$COV MATRIX=R UNCONDITIONAL
$TABLE          ID TIME WEIGHT TMAX CMAX CMAXDER ZRCROSSDIR TMAXC CMAXC IPRED NOAPPEND NOPRINT FORMAT=S1PE15.8 FILE=cmax14.tab
