$PROB  AD3TR4
$INPUT  C SET ID JID TIME CONC AMT RATE EVID MDV CMT DV LOQ TYPE

$DATA  ad3tr4.csv IGNORE = C
$SUBROUTINES ADVAN3 TRANS4

$PK
MU_1 = THETA(1)
MU_2 = THETA(2)
MU_3 = THETA(3)
MU_4 = THETA(4)
CL = EXP(MU_1 + ETA(1))
V1 = EXP(MU_2 + ETA(2))
Q = EXP(MU_3 + ETA(3))
V2 = EXP(MU_4 + ETA(4))
S1=V1

$ERROR
SD = THETA(5)
IPRED = LOG(F)
DUM = (LOQ - IPRED) / SD
CUMD = PHI(DUM)
IF (TYPE .EQ. 1) THEN
      F_FLAG = 0
      Y = IPRED + SD * ERR(1)
ENDIF
IF (TYPE .EQ. 2) THEN
      F_FLAG = 1
      Y = CUMD
      MDVRES=1
ENDIF

$THETA 
( 2.0) ;[LN(CL)]
(2.0) ;[LN(V1)]
( 2.0) ;[LN(Q)]
( 2.0) ;[LN(V2)]
 0.2
;INITIAL values of OMEGA
$OMEGA BLOCK(4)
0.15   ;[P]
0.01  ;[F]
0.15   ;[P]
0.01  ;[F]
0.01  ;[F]
0.15   ;[P]
0.01  ;[F]
0.01  ;[F]
0.01  ;[F]
0.15   ;[P]
;Initial value of SIGMA
$SIGMA 
1.0 FIXED   ;[P]

$EST METHOD=COND INTERACTION LAPLACE MAXEVAL=9999 NSIG=3 SIGL=9 SIGLO=12 PRINT=5 NOABORT
$COV MATRIX=S PRINT=E UNCONDITIONAL
$TABLE ID TIME DV IPRED PRED RES CPRED CWRES NPDE CIWRES NOAPPEND ONEHEADER ESAMPLE=1000
 FILE=ad3tr4.TAB NOPRINT
