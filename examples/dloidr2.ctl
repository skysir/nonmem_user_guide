;DDE
;TAU1=5.0
;TSTOP=100
;$SIZES MAXNDRS=1 ; SUGGESTED VALUE FOR MAXNRDS.  UNCOMMENT TO USE
;$SIZES PC=70 DIMNEW=3000 PG=100
$PROBLEM PDLIDR
$ABBR FUNCTION DDEFUNC(VDDE,4)
$ABBR PROTECT
$ABBR DERIV2=NO DERIV2=NOCOMMON ; DERIV1=NO
$INPUT ID TIME DV MDV CMT
$DATA data_LDL_Cholesterol.csv IGNORE=@
$SUBROUTINES ADVAN16 TOL=12
$MODEL NCOMPARTMENTS=3

$PK
Imax=THETA(1)
IC50=THETA(2)
Gam=THETA(3)
RZ0=THETA(4)
kout=THETA(5)
;  TAUy
TAU1=THETA(6)
; Initial conditions
A_0(1)=0
A_0(2)=0
A_0(3)=RZ0


TSTOP=100
; INITIALIZING EQUATIONS FOR DDE COMPARTMENTS
TAU_1=1*TAU1

$DES

; AD_x_y is the State value of A(x) delayed for time TAUy.
; AP_x_y is the State value of A(x) in the past, for time delay TAUy.

; DELAY SETUP FOR EQUATION SET 1
 AP_3_1=RZ0
 DTAU_1=0.0
 IF(T>=TAU_1) DTAU_1=1.0
VDDE(1)=TAU_1
VDDE(2)=T
VDDE(3)=3
VDDE(4)=1
 AZ_3_1=DDEFUNC(VDDE)
 AD_3_1=(1.0-DTAU_1)*AP_3_1+DTAU_1*AZ_3_1

; DELAY EQUATIONS FOR EQUATION SET 0 (BASE EQUATIONS)

;BASE EQUATIONS
 KIN=RZ0*KOUT
 DM=50*0.3
 KA1=1.255
 KA2=0.219
 FZ=0.214
 FR=0.715
 KEL=5.57
 K12=3.61
 K21=2.84
 V=0.719*0.3

 CC=(A(1)/V)*1000

 DADT(1)=KA1*DM*FZ*FR*EXP(-KA1*T)+KA2*DM*FZ*(1-FR)*EXP(-KA2*T)-(KEL+K12) &
  *A(1)+K21*A(2)
 DADT(2)=K12*A(1)-K21*A(2)
 DADT(3)=KIN*A(3)-KOUT*(1-(IMAX*(CC**GAM))/((IC50**GAM)+(CC**GAM)))*A(3)*AD_3_1


$ERROR
IPRED = A(3)
IRES = DV-IPRED
W = SQRT((THETA(8)*IPRED)**2+THETA(7)**2)
IWRES =  IRES/W
Y = IPRED+W*ERR(1)

$THETA
1 FIX       ; 1: Imax
(0,100)     ; 2: IC50
0.2 FIX     ; 3: Gam
(0,31.3)    ; 4: RZ0
(0,0.0065)  ; 5: kout
(0,7.12)    ; 6: T
0 FIX       ; 7: Add
(0,1)       ; 8: Prop

$OMEGA
1 FIX

$EST METHOD=0 NOHABORT MAXEVAL=9999 PRINT=1 NSIG=3
$COV UNCONDITIONAL
$TABLE ID TIME IPRED NOPRINT ONEHEADER
FILE=DLoIDR2.tab
