


 +--------------------------------------------------------------------+
 |                                                                    |
 |                    PK_PD_SIMULTANEOUS_3 EXAMPLE                    |
 |                                                                    |
 +--------------------------------------------------------------------+

 This  is  an  example  for  simultaneous analysis of PK and direct PD.
 Kinetics are two-compartment.  Either ADVAN5 or ADVAN7 could be  used.
 (See PK_PD_simultaneous_1, PK_PD_simultaneous_2).

 $PROBLEM   SIMULTANEOUS PK/PD - ADVAN7
 ; NOTE: MODEL FOR CP CAN BE MORE COMPLEX BY ADDING CMPTS
 ;
 ; THE DATA FILE CONTAINS BOTH CP AND EFFECT OBSERVATIONS.
 ; WHEN DV IS A CP OBSERVATION, CMT = 1 (OR 0),
 ; WHEN DV IS AN EFFECT OBSERVATION, CMT =3.
 $DATA   data
 $INPUT  ID TIME DV AMT=DOSE CMT
 $SUBROUTINES ADVAN7
 $MODEL
   COMP=(CENTRAL,DEFDOSE,DEFOBS)
   COMP=PERIPH
   COMP=EFFECT
 $PK
   K10= THETA(1)*EXP(ETA(1))
   K12= THETA(2)*EXP(ETA(2))
   K13= .001*K10                   ; TRIVIAL LOSS TO EFFECT COMPT
   K21= THETA(3)*EXP(ETA(3))
   S1=  THETA(4)*EXP(ETA(4))       ; V1 FOR DRUG
   K30= THETA(5)*EXP(ETA(5))       ; KEO
   EMAX=THETA(6)*EXP(ETA(6))
   C50= THETA(7)*EXP(ETA(7))
   S3=S1*K13/K30                   ; SO THAT CESS = CPSS
 $ERROR
    IF (CMT.EQ.3) THEN
     Y =EMAX*F/(C50+F)*EXP(ERR(2)) ; CMT = 3; DV = EFFECT
    ELSE
     Y =  F*EXP(ERR(1))            ; CMT = 1 (OR 0); DV = CP
    ENDIF
 ; ADD APPROPRIATE $THETA, $OMEGA, $SIGMA, $ESTIMATION, ETC.

 REFERENCES: Guide IV, section V.C.6 
 REFERENCES: Guide VI, section IV.B.2 , VII.C.7 
