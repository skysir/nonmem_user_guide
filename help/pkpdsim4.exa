


 +--------------------------------------------------------------------+
 |                                                                    |
 |                    PK_PD_SIMULTANEOUS_4 EXAMPLE                    |
 |                                                                    |
 +--------------------------------------------------------------------+

 This  is  an  example for simultaneous analysis of PK and indirect PD,
 where the PREDPP PK prediction is modified by $ERROR code.

 $PROB WARFARIN
 $INPUT ID TIME DV AMT CMT
 ; DV = DRUG CONC OR  EFFECT (PROTHROMBIN TIME)
 $DATA data
 $SUBROUTINE ADVAN6 TOL=5
 $MODEL
      COMP=(CENTRAL,DEFDOSE,DEFOBS)
      ; CAN ADD ADD'L COMPARTMENTS IF PK MULTICOMPARTMENTAL
      COMP=(CLOTFAC)
 $PK
      K10=  THETA(1)*EXP(ETA(1))      ;LOSS RATE DRUG
      S1=   THETA(2)*EXP(ETA(2))      ;VD DRUG
      SYNTH=THETA(3)*EXP(ETA(3))      ;SYNTH RATE OF CLOTING FACTOR
      LOSS= SYNTH
      C50=  THETA(4)*EXP(ETA(4))
 $DES
      DADT(1)=-K10*A(1)
      EFF    = C50/(C50+A(1)/S1)
      DADT(2)= SYNTH*EFF - LOSS*A(2)  ;CF LEVEL
 $ERROR
      PT = THETA(5)/(THETA(6)+F)      ; HYPERBOLIC MODEL FOR PRO-TIME
      IF (CMT.EQ.2) THEN
        Y = PT*EXP(EPS(2))            ; OBS IS PRO-TIME
      ELSE
        Y = F*EXP(EPS(1))             ; OBS IS DRUG CONCENTRATION
      ENDIF
 ; ADD APPROPRIATE $THETA, $OMEGA, $SIGMA, $ESTIMATION, ETC.

 The system should be initialized with a bolus dose of AMT=1 into  com-
 partment  2  (CLOTFAC) at time zero to establish the initial condition
 A(1)=0, A(2)=1 at time zero.  This  is  appropriate  because,  in  the
 absence  of  drug,  the  system  is  assumed  to  be  at  steady-state
 (DADT(2)=0).

 REFERENCES: Guide IV, section V.C.6 , V.C.7 
 REFERENCES: Guide VI, section IV.B.2 , VII.C.6 
